plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "com.jfrog.bintray" version "1.8.5"
}

ext {
    releaseVersion = System.getenv('RELEASE_VERSION') ?: '1.0.0'
}

group 'solution.s4y.rasat'
version releaseVersion

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

repositories {
    jcenter()
    mavenCentral()
}

publishing {
    publications {
        RasatPublication(MavenPublication) {
            groupId = 'solutions.s4y.rasat'
            artifactId = 'rasat'
            version = releaseVersion

            from components.java
            pom {
                name = 'Rasat Java'
                description = 'A lightweight pub-sub java library inspired by iOS Rasat'
                url = 'https://github.com/s4ysolutions/rasat-java'
                licenses {
                    license {
                        name = 'MIT License (MIT)'
                        url = 'https://github.com/s4ysolutions/rasat-java/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'dsame'
                        name = 'Sergey Dolin'
                        email = 'sergey@s4y.solutions'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/s4ysolutions/rasat-java.git'
                    developerConnection = 'scm:git:ssh://github.com/s4ysolutions/rasat-java.git'
                    url = 'https://github.com/s4ysolutions/rasat-java'
                }
            }
        }
    }
}

bintray {
    dryRun = false
    publish = true
    override = true
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['RasatPublication']

    pkg {
        repo = 'maven'
        name = 'rasat'
        userOrg = 's4ysolutions'
        desc = 'A lightweight implementation of pub-sub/observer pattern for Java'
        publications = ['RasatPublication']
        licenses = ['MIT']
        vcsUrl = 'https://github.com/s4ysolutions/rasat-java.git'
        websiteUrl = 'https://github.com/s4ysolutions/rasat-java'
        issueTrackerUrl = 'https://github.com/s4ysolutions/rasat-java/issues'
        githubRepo = 's4ysolutions/rasat-java'
        githubReleaseNotesFile = 'README.md'
        version {
            name = releaseVersion
            desc = 'Rasat Java v' + releaseVersion
            released = new Date()
            vcsTag = releaseVersion
        }
    }
}
